tools:
do;
    pr$buf: procedure (addr) external;
        declare addr address;
    end pr$buf;

    wr$con: procedure (ch) external;
        declare ch byte;
    end wr$con;

    str$eq: procedure(p$s1, p$s2) byte public;
        declare p$s1 address;
        declare s1 based p$s1 byte;
        declare p$s2 address;
        declare s2 based p$s2 byte;
        declare i byte;
        i = 0;
        do while s1(i) = s2(i) and s1(i) <> 0 and s2(i) <> 0;
            i = i + 1;
        end;
        if s1(i) = 0 and s2(i) = 0 then
            return 1;
        else
            return 0;
    end str$eq;

    str$cpy: procedure(p$src, p$dest, len) public;
        declare p$src address;
        declare p$dest address;
        declare len byte;
        declare src based p$src byte;
        declare dest based p$dest byte;
        declare i byte;
        do i = 0 to len - 1;
            dest(i) = src(i);
        end;
    end str$cpy;

    str$num: procedure(p$s) address public;
        declare p$s address;
        declare s based p$s byte;
        declare n address;
        n=0;
        do while s(0) <> 0;
            n = n * 10 + s(0) - '0';
            s = s + 1;
        end;
        return n;
    end str$num;

    pr0str: procedure(p$str) public;
        declare p$str address;
        declare str based p$str byte;
        declare i byte;
        i = 0;
        do while str(i) <> 0;
            call wr$con(str(i));
            i = i + 1;
        end;
    end pr0str;

    str0len: procedure(p$str) byte public;
        declare p$str address;
        declare str based p$str byte;
        declare i byte;
        i = 0;
        do while str(i) <> 0;
            i = i + 1;
        end;
        return i;
    end str0len;

    pr$digit: procedure(n) public;
        declare n byte;
        n = n mod 10;
        call wr$con(n + '0');
    end pr$digit;

    pr$num: procedure(n) public;
        declare n address;
        declare a(5) byte;
        declare i byte;
        i = 0;
        if n = 0 then
            call pr$digit(0);
        do while n > 0;
            a(i) = n mod 10;
            n = n / 10;
            i = i + 1;
        end;
        do while i > 0;
            call pr$digit(a(i-1));
            i = i - 1;
        end;
    end;

    declare mem$ptr address initial(0);
    declare MEM$SZ literally '0100h';
    declare mem$buf(MEM$SZ) byte;

    /* Allocates memory from mem$buf. */
    /* Returns address of allocated space of n bytes. */
    /* If not enough memory, returns 0 */
    mem$alloc: procedure(n) address public;
        declare n byte;
        declare res address;
        if mem$ptr + n > MEM$SZ then
            return 0;
        res = .mem$buf(mem$ptr);
        mem$ptr = mem$ptr + n;
        return res;
    end mem$alloc;
end tools;
