class Value {
    field integer type;
    field String stringValue;
    field integer integerValue;
    field boolean booleanValue;

    constructor Value newString(String s) {
        let type = Const.T_STRING();
        let stringValue = s;
        return this;
    }

    constructor Value newInteger(integer i) {
        let type = Const.T_INTEGER();
        let integerValue = i;
        return this;
    }

    constructor Value newBoolean(boolean b) {
        let type = Const.T_BOOLEAN();
        let booleanValue = b;
        return this;
    }

    constructor Value copy(Value value) {
        let type = value.getType();
        if (type = Const.T_STRING()) {
            let stringValue = Tools.stringCopy(value.getStringValue());
        } else {
            let integerValue = value.getIntegerValue();
            let booleanValue = value.getBooleanValue();
        }
        return this;
    }

    method integer getType() {
        return type;
    }

    method String getStringValue() {
        return stringValue;
    }

    method integer getIntegerValue() {
        return integerValue;
    }

    method boolean getBooleanValue() {
        return booleanValue;
    }

    method void print() {
        var String intString;
        if (type = Const.T_STRING()) {
            do Display.printString(stringValue);
        }
        if (type = Const.T_INTEGER()) {
            let intString = String.new(Const.SCREEN_WIDTH());
            do intString.setInt(integerValue);
            do Display.printString(intString);
            do intString.dispose();
        }
        if (type = Const.T_BOOLEAN()) {
            if (booleanValue) {
                do Display.printConst("true");
            } else {
                do Display.printConst("false");
            }
        }
        return;
    }

    method void println() {
        do print();
        do Display.newline();
        return;
    }

    method void dispose() {
        if (type = Const.T_STRING()) {
            do Memory.deAlloc(stringValue);
        }
        do Memory.deAlloc(this);
        return;
    }
}
