fwrite:
do;
    open: procedure(fcb) byte external;
        declare fcb address;
    end open;

    make: procedure(fcb) byte external;
        declare fcb address;
    end make;

    close: procedure(fcb) byte external;
        declare fcb address;
    end close;

    wr$dsk: procedure(fcb) byte external;
        declare fcb address;
    end wr$dsk;

    stdma: procedure(buf) external;
        declare buf address;
    end stdma;

    declare dfcb(33) byte initial (0);
    declare dma(128) byte;
    declare cnt byte;

    /* filename in NNNNNNNNEEE format */
    set$fn: procedure(pfn);
        declare pfn address;
        declare fn based pfn byte;
        declare i byte;
        do i = 0 to 10;
            dfcb(i + 1) = fn(i);
        end;
        cnt = 0;
    end set$fn;

    opw$fn: procedure(pfn) byte public;
        declare pfn address;
        call set$fn(pfn);
        if open(.dfcb) = 0FFH then
        do;
            if make(.dfcb) = 0FFH then
                return 0FFH;
        end;
        return 0;
    end opw$fn;

    fwstr: procedure(pstr) byte public;
        declare pstr address;
        declare str based pstr byte;
        declare i byte;
        i = 0;
        call stdma(.dma);
        do while str(i) <> 0 and i < 128;
            dma(cnt) = str(i);
            cnt = cnt + 1;
            if cnt = 128 then
            do;
                if wr$dsk(.dfcb) <> 0 then
                    return 0FFH;
                cnt = 0;
            end;
            i = i + 1;
        end;
        return 0;
    end fwstr;

    fwln: procedure(pstr) byte public;
        declare pstr address;
        if fwstr(pstr) = 0 then
            return fwstr(.(0DH, 0AH, 0));
        else
            return 0FFH;
    end fwln;

    clwf: procedure byte public;
        if cnt > 0 then
        do;
            do while cnt < 128;
                dma(cnt) = 01AH;
                cnt = cnt + 1;
            end;
            if wr$dsk(.dfcb) <> 0 then
                return 0FFH;
        end;
        if close(.dfcb) <> 0 then
            return 0FFH;
        return 0;
    end clwf;

end fwrite;
